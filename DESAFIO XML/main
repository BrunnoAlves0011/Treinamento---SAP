*&---------------------------------------------------------------------*
*& Report YDESAFIOXML_BH
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT ydesafioxml_bh.

TYPE-POOLS abap.

DATA: gcl_xml TYPE REF TO cl_xml_document.

TYPES: BEGIN OF ty_tab,
         name  TYPE string,
         value TYPE string,
       END OF ty_tab.

DATA: v_subrc      TYPE sysubrc,
      v_node       TYPE REF TO if_ixml_node,
      v_child_node TYPE REF TO if_ixml_node,
      v_root       TYPE REF TO if_ixml_node,
      v_iterator   TYPE REF TO if_ixml_node_iterator,
      v_nodemap    TYPE REF TO if_ixml_named_node_map,
      v_count      TYPE i,
      v_index      TYPE i,
      v_attr       TYPE REF TO if_ixml_node,
      v_name       TYPE string,
      v_prefix     TYPE string,
      v_value      TYPE string,
      v_char       TYPE char2,
      v_contra     TYPE i,
      str          TYPE string.

DATA: it_itab  TYPE TABLE OF ty_tab,
      wa_itab  LIKE LINE OF it_itab,
      wa_itab1 LIKE LINE OF it_itab.

DATA: it_fieldcat TYPE slis_t_fieldcat_alv,
      wa_fieldcat TYPE slis_fieldcat_alv,
      it_fcat     TYPE lvc_t_fcat,
      wa_fcat     TYPE lvc_s_fcat,
      it_layout   TYPE slis_layout_alv.

DATA: dy_table TYPE REF TO data,
      dy_line  TYPE REF TO data,
      dy_line1 TYPE REF TO data.

FIELD-SYMBOLS: <t_paymord> TYPE STANDARD TABLE,
               <l_line>, <l_line1>, <fs>.

PARAMETERS: p_filnam TYPE localfile OBLIGATORY. "DEFAULT 'C:\Users\note1\Downloads\test.xml'.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_filnam.

  DATA: l_v_filename TYPE dynfnam.

  l_v_filename = p_filnam.

  CALL FUNCTION 'F4_FILENAME'
    EXPORTING
      program_name  = syst-cprog
      dynpro_number = syst-dynnr
      field_name    = l_v_filename
    IMPORTING
      file_name     = p_filnam.

START-OF-SELECTION.
  CREATE OBJECT gcl_xml.

*Upload XML File
  CALL METHOD gcl_xml->import_from_file
    EXPORTING
      filename = p_filnam
    RECEIVING
      retcode  = v_subrc.

  CHECK v_subrc = 0.

  v_node = gcl_xml->m_document.

  CHECK NOT v_node IS INITIAL.

  v_iterator = v_node->create_iterator( ).
  v_node = v_iterator->get_next( ).

  WHILE NOT v_node IS INITIAL.
    CASE v_node->get_type( ).

      WHEN if_ixml_node=>co_node_element.
        v_name = v_node->get_name( ).
        v_nodemap = v_node->get_attributes( ).

        IF NOT v_nodemap IS INITIAL.

          v_count = v_nodemap->get_length( ).

          DO v_count TIMES.

            v_index = sy-index + 1.
            v_attr = v_nodemap->get_item( v_index ).
            v_name = v_attr->get_name( ).
            v_prefix = v_attr->get_namespace_prefix( ).
            v_value = v_attr->get_value( ).

          ENDDO.
        ENDIF.

      WHEN if_ixml_node=>co_node_text OR if_ixml_node=>co_node_cdata_section.

        v_value = v_node->get_value( ).
        MOVE v_value TO v_char.

        IF v_char <> cl_abap_char_utilities=>cr_lf.

          wa_itab-name = v_name.
          wa_itab-value = v_value.
          APPEND wa_itab TO it_itab.
          CLEAR wa_itab.

        ENDIF.
    ENDCASE.

    v_node = v_iterator->get_next( ).

  ENDWHILE.

END-OF-SELECTION.
  PERFORM monta_tabela.
  PERFORM cria_tab_dinamica.
  PERFORM monta_dados.
*  BREAK-POINT.
  PERFORM display.

FORM monta_tabela.

  DATA: pos      TYPE i,
        primeiro TYPE string.

  CLEAR: pos.
  it_layout-colwidth_optimize = 'X'.
  READ TABLE it_itab INTO wa_itab INDEX 1.
  primeiro = wa_itab-name.

  LOOP AT it_itab INTO wa_itab.
    pos = pos + 1.
    IF primeiro = wa_itab-name AND pos > 1.
      str = wa_itab-name.
      EXIT.
    ELSE.
      wa_fcat-tabname = 'IT_ITAB'.
      wa_fcat-fieldname = wa_itab-name.
      wa_fcat-coltext = wa_itab-name.
      wa_fcat-outputlen = 200.
      APPEND wa_fcat TO it_fcat.
      CLEAR wa_fcat.
      wa_fieldcat-tabname = 'IT_ITAB'.
      wa_fieldcat-fieldname = wa_itab-name.
      wa_fieldcat-seltext_l = wa_itab-name.
      wa_fieldcat-outputlen = 20.
      wa_fieldcat-col_pos = pos.
      APPEND wa_fieldcat TO it_fieldcat.
      CLEAR wa_fieldcat.
      v_contra = v_contra + 1.
    ENDIF.
  ENDLOOP.

ENDFORM.

FORM cria_tab_dinamica.

 CALL METHOD cl_alv_table_create=>create_dynamic_table
    EXPORTING
      i_style_table             = 'X'
      it_fieldcatalog           = it_fcat
    IMPORTING
      ep_table                  = dy_table
    EXCEPTIONS
      generate_subpool_dir_full = 1
      OTHERS                    = 2.

  ASSIGN dy_table->* TO <t_paymord>.

  CREATE DATA dy_line  LIKE LINE OF <t_paymord>.
  CREATE DATA dy_line1 LIKE LINE OF <t_paymord>.

  ASSIGN dy_line->* TO <l_line>.
  ASSIGN dy_line1->* TO <l_line1>.

ENDFORM.

FORM monta_dados.
  DATA: pos      TYPE i,
        primeiro TYPE string,
        pos1     TYPE i,
        v_p1     TYPE i.

  CLEAR: pos.

  READ TABLE it_itab INTO wa_itab INDEX 1.
  primeiro = wa_itab-name.

  LOOP AT it_itab INTO wa_itab.
    IF primeiro = wa_itab-name.
      pos = pos + 1.
    ENDIF.
  ENDLOOP.

  v_p1 = 1.

  WHILE pos1 <> pos.
    DO v_contra TIMES.

      READ TABLE it_itab INTO wa_itab INDEX v_p1.
      ASSIGN COMPONENT wa_itab-name OF STRUCTURE <l_line> TO <fs>.
      <fs> = wa_itab-value.
      UNASSIGN <fs>.
      v_p1 = v_p1 + 1.

    ENDDO.

    pos1 = pos1 + 1.
    APPEND <l_line> TO <t_paymord>.

  ENDWHILE.

ENDFORM.

FORM display.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program = sy-repid
      it_fieldcat        = it_fieldcat
      i_default          = 'X'
      is_layout          = it_layout
    TABLES
      t_outtab           = <t_paymord>.

ENDFORM.